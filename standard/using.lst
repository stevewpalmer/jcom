10 DIM filename1$ OF 20, filename2$ OF 20
20 DIM reply$ OF 1
30 DIM text1$ OF 40, text2$ OF 40 
40 filename1$:="uqtestzp.dat"; filename2$:="uqtestzu.dat" 
50 PRINT "PRINT USING TEST"
60 PRINT "This program uses two disk files"
70 PRINT "that it creates, uses, then deletes,"
80 PRINT filename1$;"and";filename2$
90 PRINT "place blank formatted disk into"
100 INPUT "current drive. Hit return when ready:": reply$
110 //
120 DELETE filename1$
130 DELETE filename2$
140 //
150 OPEN FILE 1,filename1$,WRITE // correct answer goes here 
160 OPEN FILE 2,filename2$,WRITE // print usings go here
170 //
180 PRINT FILE 1:"test 120.0 test"
190 PRINT FILE 2: USING "test ###.# test": 120
200 //
210 PRINT FILE 1:"test   5.47 test"
220 PRINT FILE 2: USING "test -##.## test": 5.467
230 //
240 PRINT FILE 1:"test  -5.47 test"
250 PRINT FILE 2: USING "test -##.## test": -5.467
260 //
270 PRINT FILE 1:"test ******* test"
280 PRINT FILE 2: USING "test ###.### test": 12345
290 //
300 PRINT FILE 1:"test   3  $ 55.00 test"
310 PRINT FILE 2: USING "test ###  $###.## test": 3,55
320 //
330 CLOSE
340 //
350 PRINT "comparing the files now"
360 PRINT
370 OPEN FILE 1,filename1$,READ
380 OPEN FILE 2,filename2$,READ
390 passed:=TRUE // init
400 WHILE NOT (EOF(1) OR EOF(2)) DO
410   INPUT FILE 1: text1$ 
420   INPUT FILE 2: text2$ 
430   PRINT text1$
440   PRINT text2$
450   PRINT "=========="
460   IF text1$<>text2$ THEN passed:=FALSE
470 ENDWHILE
480 CLOSE
490 DELETE filename1$
500 DELETE filename2$
510 IF passed THEN
520   PRINT "====> All passed" 
530 ELSE 
540   PRINT "====> Failed" 
550 ENDIF
